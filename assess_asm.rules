####################
## Assembly assess and comparison
## MUMmer + QUAST (+ minimap2)
####################

rule _all_eval:
 input:
  expand(config['project_dir'] + '/reports/09_eval/dnadiff/{barcode}/{prefix}.report',
  barcode=BARCODES, prefix = ['ref_draft1', 'ref_draft2']),
  expand(config['project_dir'] + '/reports/09_eval/nucmer/{barcode}/{prefix}.coords',
  barcode=BARCODES, prefix = ['ref_draft1','ref_draft2','draft1_draft2']),
  expand(config['project_dir'] + '/reports/09_eval/quast_results/{barcode}/quast.log', barcode=BARCODES)

rule final_contigs:
 input:
  pilon = config['project_dir'] + '/07_pilon/{barcode}/pilon.fasta',
  unicycler = config['project_dir'] + '/08_unicycler/{barcode}/assembly.fasta'
 output:
  draft1 = config['project_dir'] + '/reports/final_assembly/{barcode}/draft1.fasta',
  draft2 = config['project_dir'] + '/reports/final_assembly/{barcode}/draft2.fasta',
 params:
   minlen = config['min_length']
 shell:
   """
   vsearch --notrunclabels --sortbylength {input.pilon} --minseqlength {params.minlen} --maxseqlength -1 --output {output.draft1} 

   vsearch --notrunclabels --sortbylength {input.unicycler} --minseqlength {params.minlen} --maxseqlength -1 --output {output.draft2} 
   """

rule eval_assembly:
 input:
  refgenome = config['project_dir'] + '/' + config['genome_fp'],
  draft1 = config['project_dir'] + '/reports/final_assembly/{barcode}/draft1.fasta',
  draft2 = config['project_dir'] + '/reports/final_assembly/{barcode}/draft2.fasta'
 output:
  repeats = config['project_dir'] + '/reports/09_eval/nucmer/{barcode}/ref.repeats',
  draft1 = config['project_dir'] + '/reports/09_eval/dnadiff/{barcode}/ref_draft1.report',
  draft2 = config['project_dir'] + '/reports/09_eval/dnadiff/{barcode}/ref_draft2.report',
  ref_draft1 = config['project_dir'] + '/reports/09_eval/nucmer/{barcode}/ref_draft1.coords',
  ref_draft2 = config['project_dir'] + '/reports/09_eval/nucmer/{barcode}/ref_draft2.coords',
  draft1_draft2 = config['project_dir'] + '/reports/09_eval/nucmer/{barcode}/draft1_draft2.coords',
 params:
  out_dir1 = config['project_dir'] + '/reports/09_eval/dnadiff/{barcode}',
  out_dir2 = config['project_dir'] + '/reports/09_eval/nucmer/{barcode}',
  prefix1 = 'ref_draft1',
  prefix2 = 'ref_draft2',
  prefix3 = 'draft1_draft2'
 shell:
  """
  ## avgident
  cd {params.out_dir1}
  dnadiff --prefix ref_draft1 {input.refgenome} {input.draft1}
  dnadiff --prefix ref_draft2 {input.refgenome} {input.draft2}
  
  cd {params.out_dir2}
  ## find exact tandem repeats of length 100 or greater in a single sequence seq.fasta
  repeat-match -n 100 {input.refgenome} > {output.repeats}

  ## mapping a draft sequence to a finished sequence
  nucmer -maxmatch -c 100 -p {params.prefix1} {input.refgenome} {input.draft1}
  show-coords -r -c -l {params.prefix1}.delta > {output.ref_draft1}
  
  nucmer -maxmatch -c 100 -p {params.prefix2} {input.refgenome} {input.draft2}
  show-coords -r -c -l {params.prefix2}.delta > {output.ref_draft2}
  
  ## aligning two draft sequences
  nucmer -maxmatch -c 100 -p {params.prefix3} {input.draft1} {input.draft2}
  show-coords -r -c -l {params.prefix3}.delta > {output.draft1_draft2}
  """

rule run_prokka:
 input:
  config['project_dir'] + '/reports/final_assembly/{barcode}/draft1.fasta'
 output:
  fna = config['project_dir'] + '/reports/09_eval/quast_results/{barcode}/prokka_pilon/draft1.fasta',
  gff = config['project_dir'] + '/reports/09_eval/quast_results/{barcode}/prokka_pilon/draft1.gff'
 params:
  outdir = config['project_dir'] + '/reports/09_eval/quast_results/{barcode}/prokka_pilon'
 threads: 4
 conda:
  config['quast_yaml_fp']
 shell:
  """
  cp {input} {output.fna}
  prokka --kingdom Bacteria --outdir {params.outdir} --prefix draft1 \
         --locustag draft1 --force {output.fna}
  """ 

rule run_quast:
 input:
  #gff = config['project_dir'] + '/reports/09_eval/quast_results/{barcode}/prokka_pilon/draft1.gff',
  ref = config['project_dir'] + '/' + config['genome_fp'],
  gff = config['project_dir'] + '/' + config['gene_fp'],
  draft1 = config['project_dir'] + '/reports/09_eval/quast_results/{barcode}/prokka_pilon/draft1.fasta',
  draft2 = config['project_dir'] + '/reports/final_assembly/{barcode}/draft2.fasta'
 output:
  config['project_dir'] + '/reports/09_eval/quast_results/{barcode}/quast.log',
 params:
  outdir = config['project_dir'] + '/reports/09_eval/quast_results/{barcode}'
 threads: 4
 conda: 
  config['quast_yaml_fp']
 shell:
  """
  quast.py -t {threads} --gene-finding --gage -o {params.outdir} \
           --min-contig 1000000 \
           -R {input.ref} -G {input.gff} {input.draft1} {input.draft2}
  """
